GCobol >>SOURCE FORMAT IS FREE
*> modified from the basic code at https://programmingpraxis.com/2010/07/27/hamurabi-bas/2/
*>*********************************************************************
IDENTIFICATION DIVISION.
PROGRAM-ID. HAMURABI3.
DATA DIVISION.
WORKING-STORAGE SECTION.
   01 RANDOM-GROUP.
       03 SEED-VALUE PIC 9(2).
       03 FRAND PIC 9(1)V9999.
       03 RANGE PIC 9(2).
       03 OFFSET  PIC 9(2).
       03 RANDOM-NUMBER PIC 9(2). 
   01 WORLD-GROUP.
   *>These have the totals
       03 ACRES PIC 9(5).
       03 POPULATION PIC 9(5).
       03 CUMULATIVE-BIRTHS PIC 9(5).
       03 CUMULATIVE-DEATHS PIC 9(5).
       03 STOREHOUSE PIC 9(5).
       03 ZYEAR PIC 9(2).
       03 AVERAGE-DEATHS PIC 9(2)V9.
       03 ACRES-PER-PERSON PIC 9(2)V9.
       03 PEOPLE-HATE PIC 9(4).
   01 YEAR-GROUP.
   *>These are the changes each year 
       03 ACRES-TO-BUY PIC S9(4).
       03 ACRES-TO-SELL PIC S9(4).
       03 COST-OF-LAND PIC 9(2).
       03 BUSHELS-TO-FEED PIC S9(4).
       03 ACRES-TO-PLANT PIC S9(4).
       03 PLANTING-ERROR PIC 9(1).
       03 YIELD PIC 9(2)V9.
       03 HARVEST PIC 9(4).
       03 IMMIGRANTS PIC 9(4).
       03 DEATHS PIC 9(4).
       *> PERCENT-STARVED, NOT JUST DIED
       03 PERCENT-STARVED PIC 9(2)V99.
       *> Plague-chance is a number from 1..99. If it is 15 or less then plague occurs
       03 PLAGUE-CHANCE PIC 9(2)V9.
       03 PLAGUE-OCCURRED PIC 9(1).
       03 PLAGUE-DEATHS PIC P(4).
       03 EATEN-BY-RATS PIC 9(4).
       03 PEOPLE-FED PIC 9(4).
       03 SPENDING PIC 9(5).       
       
PROCEDURE DIVISION.
MAIN SECTION.
   PERFORM INIT-RANDOMIZER.
   PERFORM INIT.
   PERFORM TURN 10 TIMES.
   PERFORM GAME-RESULTS.
   
INIT-RANDOMIZER SECTION.
   MOVE FUNCTION NUMVAL(FUNCTION CURRENT-DATE(15:2)) TO SEED-VALUE.
   COMPUTE FRAND = FUNCTION RANDOM(SEED-VALUE).
   
GET-RANDOM-NUMBER SECTION.
   COMPUTE FRAND = FUNCTION RANDOM().
   COMPUTE RANDOM-NUMBER = FRAND * RANGE + OFFSET.
   *>DISPLAY RANDOM-NUMBER.

INIT SECTION.
   DISPLAY "HAMURABI ".
   DISPLAY "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY. " END-DISPLAY.
   DISPLAY "TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA ".          
   DISPLAY "SUCCESSFULLY FOR A TEN-YEAR TERM OF OFFICE.". 
   MOVE 1000 TO ACRES.
   MOVE 95 TO POPULATION.
   MOVE 2800 TO STOREHOUSE.
   MOVE 3000 TO HARVEST.
   MOVE 200 TO EATEN-BY-RATS.
   MOVE 3 TO YIELD.
   MOVE 5 TO IMMIGRANTS.
   MOVE 99.9 TO PLAGUE-CHANCE.

TURN SECTION.
   ADD 1 TO ZYEAR.
   PERFORM ANNUAL-REPORT.
   PERFORM GET-INPUT.
   PERFORM SCENARIOS.
   PERFORM RUNNING-TOTALS.

ANNUAL-REPORT SECTION.
   COMPUTE POPULATION = POPULATION + IMMIGRANTS - DEATHS - PLAGUE-DEATHS.
   DISPLAY "HAMURABI:  I BEG TO REPORT TO YOU ".
   DISPLAY "IN YEAR ",ZYEAR,":".
   DISPLAY DEATHS," PEOPLE STARVED, AND ",IMMIGRANTS," CAME TO THE CITY. ".
   IF PLAGUE-OCCURRED = 1 THEN
       DISPLAY "A HORRIBLE PLAGUE STRUCK!  HALF THE PEOPLE DIED."
   END-IF.   
   DISPLAY "POPULATION IS NOW ",POPULATION,". ".
   DISPLAY "THE CITY NOW OWNS ",ACRES," ACRES. ".
   DISPLAY "YOU HARVESTED ",YIELD," BUSHELS PER ACRE. ".
   DISPLAY "RATS ATE ",EATEN-BY-RATS," BUSHELS. ".
   DISPLAY "YOU NOW HAVE ",STOREHOUSE," BUSHELS IN STORE.". 

 GET-INPUT SECTION.
   *> Calculate the cost of land
   MOVE 10 TO RANGE.
   MOVE 1 TO OFFSET.
   PERFORM GET-RANDOM-NUMBER.
   COMPUTE COST-OF-LAND = RANDOM-NUMBER + 17.
   DISPLAY "LAND IS TRADING AT ",COST-OF-LAND, " BUSHELS PER ACRE. ".
   PERFORM GET-BUY.
   IF ACRES-TO-BUY > 0 THEN
       COMPUTE ACRES = ACRES + ACRES-TO-BUY
       COMPUTE STOREHOUSE = STOREHOUSE - ( ACRES-TO-BUY * COST-OF-LAND )
       *>DISPLAY "[STOREHOUSE: ",STOREHOUSE,"]"
   ELSE
       PERFORM GET-SELL
       IF ACRES-TO-SELL> 0 THEN
           COMPUTE ACRES = ACRES - ACRES-TO-SELL
           COMPUTE STOREHOUSE = STOREHOUSE + ( ACRES-TO-SELL * COST-OF-LAND )
           *>DISPLAY "[STOREHOUSE: ",STOREHOUSE,"]"
       END-IF
    END-IF.
    PERFORM FEED-PEOPLE.
    PERFORM PLANT-SEED.

*> GET A NON-NEGATIVE NUMBER FOR ACRES TO BUY
GET-BUY SECTION.
   *> If the cost is more than we have then show grain error and perform it again.
   MOVE 0 TO ACRES-TO-BUY.
   MOVE 0 TO SPENDING.
   PERFORM WITH TEST AFTER UNTIL SPENDING <= STOREHOUSE
       DISPLAY "HOW MANY ACRES DO YOU WISH TO BUY?" END-DISPLAY
       ACCEPT ACRES-TO-BUY
       IF ACRES-TO-BUY < 0 THEN PERFORM STEWARD-RESIGN END-IF
       COMPUTE SPENDING = ACRES-TO-BUY * COST-OF-LAND
       IF SPENDING > STOREHOUSE THEN PERFORM GRAIN-ERROR END-IF
   END-PERFORM.
    
GET-SELL SECTION.
   PERFORM WITH TEST AFTER UNTIL ACRES-TO-SELL <= ACRES
   DISPLAY "HOW MANY ACRES DO YOU WISH TO SELL?"
        ACCEPT ACRES-TO-SELL
        IF ACRES-TO-SELL < 0 THEN PERFORM STEWARD-RESIGN
   END-PERFORM.

*>REM *** TRYING TO USE MORE GRAIN THAN IN THE SILOS?
FEED-PEOPLE SECTION.
   *>DISPLAY "[FEED PEOPLE]".
   MOVE 0 TO BUSHELS-TO-FEED.
   PERFORM WITH TEST AFTER UNTIL BUSHELS-TO-FEED <= STOREHOUSE
       DISPLAY "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE"
       ACCEPT BUSHELS-TO-FEED
       IF BUSHELS-TO-FEED < 0 THEN PERFORM STEWARD-RESIGN END-IF
       IF BUSHELS-TO-FEED > STOREHOUSE THEN PERFORM GRAIN-ERROR END-IF
   END-PERFORM.
   COMPUTE STOREHOUSE = STOREHOUSE - BUSHELS-TO-FEED.
   *>DISPLAY "[STOREHOUSE: ",STOREHOUSE,"]".
   *>REM *** HOW MANY PEOPLE HAD FULL TUMMIES?
   COMPUTE PEOPLE-FED = BUSHELS-TO-FEED / 20.
   *>DISPLAY "[PEOPLE-FED: ",PEOPLE-FED,"]".

*>---------------------------------------
*>Planting seed
*> There are 4 error conditions here:
*> 1. Trying to plant more acres than you own
*> 2. Trying to plant less than 0 acres
*> 3. Not having enough grain for seed
*>         Each bushel can be used to plant 2 acres
*> 4. Not having enough people to tend the crops
*>         Each person can plant 10 acres
*>Error conditions are stored in PLANTING-ERROR
*>---------------------------------------
PLANT-SEED SECTION.
     PERFORM WITH TEST AFTER UNTIL PLANTING-ERROR = 0
          PERFORM PLANT-SEED-ERRORS
*>get input
          DISPLAY "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED?"
          ACCEPT ACRES-TO-PLANT
          *>DISPLAY "[ACRES-TO-PLANT: ",ACRES-TO-PLANT,"]"
          PERFORM PLANT-SEED-CHECK
          *>DISPLAY "[PLANTING-ERROR: ",PLANTING-ERROR,"]"
    END-PERFORM.
    COMPUTE STOREHOUSE = STOREHOUSE - (ACRES-TO-PLANT / 2).
    *>DISPLAY "[STOREHOUSE: ",STOREHOUSE,"]".

PLANT-SEED-ERRORS SECTION.
   *>Give error message if any.  Could be done in a switch statement
   IF PLANTING-ERROR = 1 THEN PERFORM ACRES-ERROR.
       *>REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
   IF PLANTING-ERROR = 2 THEN PERFORM STEWARD-RESIGN.
   IF PLANTING-ERROR = 3 THEN PERFORM GRAIN-ERROR.
   IF PLANTING-ERROR = 4 THEN PERFORM PEOPLE-ERROR.
               

PLANT-SEED-CHECK SECTION.
   *>test inpuT
   *>444 REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
   IF ACRES-TO-PLANT > ACRES THEN MOVE 1 TO PLANTING-ERROR.
   IF ACRES-TO-PLANT < 0 THEN MOVE 2 TO PLANTING-ERROR. 
   *>449 REM *** ENOUGH GRAIN FOR SEED?
   IF (ACRES-TO-PLANT / 2) > STOREHOUSE THEN MOVE 3 TO PLANTING-ERROR. 
   *>454 REM *** ENOUGH PEOPLE TO TEND THE CROPS?
   IF ACRES-TO-PLANT > (POPULATION * 10) THEN MOVE 4 TO PLANTING-ERROR.

SCENARIOS SECTION.
   *>#1 How much was the harvest?
   MOVE 5 TO RANGE.
   MOVE 1 TO OFFSET.
   PERFORM GET-RANDOM-NUMBER.
   MOVE RANDOM-NUMBER TO YIELD.
   COMPUTE HARVEST =  ACRES-TO-PLANT * YIELD.  

   *>#2 How much did the rats eat?
   PERFORM GET-RANDOM-NUMBER.
   IF RANDOM-NUMBER > 1 THEN 
       COMPUTE EATEN-BY-RATS = STOREHOUSE / RANDOM-NUMBER
   END-IF.
   
   *>#3 COMPUTE NEW STOREHOUSE TOTAL
   COMPUTE STOREHOUSE = STOREHOUSE - EATEN-BY-RATS + HARVEST.
   *>DISPLAY "[STOREHOUSE: ",STOREHOUSE,"]"

   *>#4 BABIES
   PERFORM GET-RANDOM-NUMBER.
   *>532 REM *** LETS HAVE SOME BABIES
   *>533 I=INT(C*(20*A+S)/P/100+1)
   *> I am using the formula as is.
   COMPUTE IMMIGRANTS = RANDOM-NUMBER * (20 * ACRES + STOREHOUSE) / POPULATION / 100 + 1 .

   *>#5 PLAGUE
   *>541 REM *** HORRORS, A 15% CHANCE OF PLAGUE
   *>542 Q=INT(10*(2*RND(1)-.3))
   MOVE 99 TO RANGE.
   MOVE 0 TO OFFSET.   
   PERFORM GET-RANDOM-NUMBER.
   MOVE RANDOM-NUMBER TO PLAGUE-CHANCE.
   IF PLAGUE-CHANCE < 15 THEN
       *> PLAGUE!
       COMPUTE PLAGUE-DEATHS = POPULATION / 2
       MOVE 1 TO PLAGUE-OCCURRED
   ELSE
       MOVE 0 TO PLAGUE-OCCURRED
   END-IF.

   *>#6 STARVE TO DEATH
   *>DISPLAY "[PEOPLE-FED: ",PEOPLE-FED,"]".
   *>DISPLAY "[POPULATION: ",POPULATION,"]".
   IF PEOPLE-FED < POPULATION THEN
       COMPUTE DEATHS = POPULATION - PEOPLE-FED
       COMPUTE PERCENT-STARVED = DEATHS / POPULATION 
       *>551 REM *** STARVE ENOUGH FOR IMPEACHMENT?
       IF PERCENT-STARVED > 0.45 THEN PERFORM IMPEACHMENT END-IF
   ELSE
       MOVE 0 TO DEATHS
       MOVE 0.0 TO PERCENT-STARVED
   END-IF.
   *>DISPLAY "[DEATHS: ",DEATHS,"]".

RUNNING-TOTALS SECTION.

   *>553 P1=((Z-1)*P1+D*100/P)/Z
   *> COMPUTE P1=((Z - 1)*P1+D*100/P)/Z.
   *> I don't understand this formula but it comes from the BASIC source   
   COMPUTE AVERAGE-DEATHS = ((ZYEAR - 1) * AVERAGE-DEATHS + DEATHS * 100 / POPULATION) / ZYEAR.
   *>555 P=C:D1=D1+D:GOTO 215  
   *>     COMPUTE P = P - D.
   *> Same as P = P - PFED.
   *> COMPUTE D1 = D1 + D.
   ADD IMMIGRANTS TO CUMULATIVE-BIRTHS.
   COMPUTE CUMULATIVE-DEATHS = CUMULATIVE-DEATHS + DEATHS + PLAGUE-DEATHS.
  
STEWARD-RESIGN SECTION.
   DISPLAY "HAMURABI: I CANNOT DO WHAT YOU WISH SINCE I DO NOT".
   DISPLAY "UNDERSTAND NEGATIVE NUMBERS.".
   DISPLAY "PROCURE YOURSELF ANOTHER STEWARD!".
   PERFORM GOODBYE.

GRAIN-ERROR SECTION.
   DISPLAY "HAMURABI:  THINK AGAIN. YOU HAVE ONLY".
   DISPLAY STOREHOUSE," BUSHELS OF GRAIN. NOW THEN,".

ACRES-ERROR SECTION.
   DISPLAY "HAMURABI:  THINK AGAIN. YOU OWN ONLY".
   DISPLAY ACRES," ACRES OF LAND. NOW THEN,".
   
PEOPLE-ERROR SECTION.
   DISPLAY "HAMURABI:  THINK AGAIN. YOU HAVE ONLY".
   DISPLAY POPULATION," PEOPLE TO TEND THE FIELDS. NOW THEN,".

IMPEACHMENT SECTION.
    DISPLAY "YOU STARVED ",DEATHS," PEOPLE IN ONE YEAR!!!".
    DISPLAY "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY".
    DISPLAY "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE".
    DISPLAY "ALSO BEEN DECLARED *NATIONAL FINK* !!".
    PERFORM GOODBYE.

GAME-RESULTS SECTION.
      COMPUTE ACRES-PER-PERSON=ACRES / POPULATION.
      DISPLAY "IN YOUR 10-YEAR TERM OF OFFICE, ",AVERAGE-DEATHS," PERCENT OF THE".
      DISPLAY "POPULATION STARVED PER YEAR ON AVERAGE, I.E., A TOTAL".
      DISPLAY "OF ",CUMULATIVE-DEATHS," PEOPLE DIED.".
      DISPLAY "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH".     
      DISPLAY  ACRES-PER-PERSON,"ACRES PER PERSON.".
      IF AVERAGE-DEATHS>33 THEN PERFORM IMPEACHMENT.
      IF ACRES-PER-PERSON<7 THEN PERFORM IMPEACHMENT.
      IF AVERAGE-DEATHS>10 THEN PERFORM HORRIBLE-PERFORMANCE.
      IF ACRES-PER-PERSON<9 THEN PERFORM HORRIBLE-PERFORMANCE.
      IF AVERAGE-DEATHS>3 THEN PERFORM MEDIOCRE-PERFORMANCE.
      IF ACRES-PER-PERSON<10 THEN 
           PERFORM MEDIOCRE-PERFORMANCE
      ELSE
           PERFORM EXCELLENT-PERFORMANCE
      END-IF.

EXCELLENT-PERFORMANCE SECTION.
      DISPLAY
           "A FANTASTIC PERFORMANCE!!!  CHARLEMANGE, DISRAELI,"
           "AND JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!"
      END-DISPLAY.
      PERFORM GOODBYE.

HORRIBLE-PERFORMANCE SECTION.
      DISPLAY
          "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND."
          "IVAN IV. THE PEOPLE REMAINING FIND YOU AN UNPLEASANT"
          "RULER, AND, FRANKLY, HATE YOUR GUTS."
      END-DISPLAY.
      PERFORM GOODBYE.

MEDIOCRE-PERFORMANCE SECTION.
      MOVE 99 TO RANGE.
      MOVE 0 TO OFFSET.
      PERFORM GET-RANDOM-NUMBER.
      COMPUTE PEOPLE-HATE = POPULATION * .8 * RANDOM-NUMBER / 100.
      DISPLAY
           "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
           "REALLY WASNT TOO BAD AT ALL. "
           PEOPLE-HATE," PEOPLE WOULD"
           "DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE"
           "OUR TRIVIAL PROBLEMS."
      END-DISPLAY.
      PERFORM GOODBYE.

GOODBYE SECTION.
   DISPLAY "SO LONG FOR NOW AND THANKS FOR ALL THE FISH.".
   DISPLAY "FAREWELL, AUF WIEDERSEHEN, GOODBYE.".
   STOP RUN.

END PROGRAM HAMURABI3. 
